SUBROUTINE WRITEMUSC(&
  &YDCPG_MISC,YDCPG_PHY0,YDMF_PHYS,YDCPG_DYN0,YDMF_PHYS_SURF,YDVARS,&
  &YDVETA, YDRIP,YDML_PHY_MF,KIDIA  , KFDIA  , KLON   ,&
  &KTDIA  , KLEV   , KGL1   , KGL2   ,&
  &KSTEP  , KSGST  , KCSS   , KTSSG  ,&
  &PCLON  , PSLON  ,&
  &PGELAM , PGEMU  , PGM    , POROG  ,&
  &PCVGQ, PMU0, PC1, PC2, PCPS, PLHS, PRS, PCD, PCDN, &
  &PCH, PEMIS, PFEVI, PNEIJ, PQSAT, PQSATS, PVEG0, KCLPH, &
  &PFRMQ, PLH, PLSCPE, PQW, PTW, PFEFB1, PFEFB2, PFEFB3, PFTKE)

!     **** *WRITEMUSC* - Ecriture des resultats de la physique.

!     Purpose.
!     --------
!     Ecriture des resultats de la physique.

!     **   Interface.
!     ----------

!     Explicit arguments :    None.
!     --------------------

!     Implicit arguments :    None.
!     --------------------

!     Method.
!     -------

!     Externals.   None.
!     ----------

!     Reference.
!     ----------

!     Author.
!     -------
!     Eric Bazile, Francois Bouyssel et Jean-Marcel Piriou
!     Original :97-02-01

!     Modifications.
!     --------------
!     Modified for MUSC 2008-04-23 Eric BAZILE
!     F. Kocaman 2011-10-04 : CY38 cleaning
!     K. Yessad (July 2014): Move some variables.
!      R. El Khatib 08-Jul-2022 Contribution to the encapsulation of YOMCST and YOETHF
!     ------------------------------------------------------------------

USE MF_PHYS_TYPE_MOD   , ONLY : MF_PHYS_TYPE
USE CPG_TYPE_MOD       , ONLY : CPG_DYN_TYPE, CPG_PHY_TYPE, &
                              & CPG_MISC_TYPE
USE MF_PHYS_SURFACE_TYPE_MOD,ONLY : MF_PHYS_SURF_TYPE
USE FIELD_VARIABLES_MOD, ONLY : FIELD_VARIABLES
USE MODEL_PHYSICS_MF_MOD , ONLY : MODEL_PHYSICS_MF_TYPE
USE YOMVERT  , ONLY : TVETA
USE PARKIND1 , ONLY : JPIM     ,JPRB
USE YOMHOOK  , ONLY : LHOOK    ,DR_HOOK, JPHOOK

USE YOMRIP0  , ONLY : NINDAT   ,NSSSSS
USE YOMRIP   , ONLY : TRIP
USE YOMCST   , ONLY : YDCST=>YRCST ! allows use of included functions. REK.
USE YOMCT0   , ONLY : LECMWF, LAROME

!     ------------------------------------------------------------------

IMPLICIT NONE

TYPE(CPG_MISC_TYPE),INTENT(IN):: YDCPG_MISC
TYPE(CPG_PHY_TYPE),INTENT(IN) :: YDCPG_PHY0
TYPE(MF_PHYS_TYPE),INTENT(IN) :: YDMF_PHYS
TYPE(CPG_DYN_TYPE),INTENT(IN) :: YDCPG_DYN0
TYPE(MF_PHYS_SURF_TYPE),INTENT(IN) :: YDMF_PHYS_SURF
TYPE(FIELD_VARIABLES),INTENT(IN) :: YDVARS
TYPE(TVETA)       ,INTENT(IN) :: YDVETA
TYPE(MODEL_PHYSICS_MF_TYPE),INTENT(IN):: YDML_PHY_MF
TYPE(TRIP)        ,INTENT(IN) :: YDRIP
INTEGER(KIND=JPIM),INTENT(IN) :: KFDIA, KGL1, KGL2, KIDIA, KSTEP
INTEGER(KIND=JPIM),INTENT(IN) :: KTDIA, KLEV, KLON
INTEGER(KIND=JPIM),INTENT(IN) :: KSGST, KCSS, KTSSG
REAL(KIND=JPRB),INTENT(IN) :: PGEMU(KLON),PGELAM(KLON),PGM(KLON),POROG(KLON)
REAL(KIND=JPRB),INTENT(IN) :: PCLON(KLON), PSLON(KLON)

REAL (KIND=JPRB), INTENT (IN)         :: PCVGQ (KLON, KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PMU0 (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PC1 (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PC2 (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PCPS (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PLHS (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PRS (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PCD (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PCDN (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PCH (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PEMIS (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PFEVI (KLON, KTSSG+1)
REAL (KIND=JPRB), INTENT (IN)         :: PNEIJ (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PQSAT (KLON, KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PQSATS (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PVEG0 (KLON)
INTEGER (KIND=JPIM), INTENT (IN)      :: KCLPH (KLON)
REAL (KIND=JPRB), INTENT (IN)         :: PFRMQ (KLON, 0:KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PLH (KLON, KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PLSCPE (KLON, KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PQW (KLON, KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PTW (KLON, KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PFEFB1 (KLON, 0:KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PFEFB2 (KLON, 0:KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PFEFB3 (KLON, 0:KLEV)
REAL (KIND=JPRB), INTENT (IN)         :: PFTKE (KLON, 0:KLEV)

!     ------------------------------------------------------------------
! ----------------------------------------------
! Postprocessing fields
! ----------------------------------------------

REAL(KIND=JPRB), EXTERNAL :: THETA,THETAL_KE,THETAV,THETAVL,HCLA
REAL(KIND=JPRB), EXTERNAL :: THETAE_BOLTON,THETAES_BOLTON
REAL(KIND=JPRB) :: ZW(KLON,KLEV)
REAL(KIND=JPRB) :: ZVENT(KLON,KLEV),ZDIRVENT(KLON,KLEV)
REAL(KIND=JPRB) :: ZVENTCLS(KLON),ZDIRCLS(KLON)
REAL(KIND=JPRB) :: ZECT(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZELEVATION(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZTHETA(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZTHETAL_KE(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZTHETAL(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZTHETAL_LES(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZTHETAV(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZTHETAVL(KLON,KLEV+1)
REAL(KIND=JPRB) :: ZQC(KLON,KLEV),ZH(KLON,KLEV)
REAL(KIND=JPRB) :: ZLWP(KLON),ZECT_INT(KLON)
REAL(KIND=JPRB) :: ZIWP(KLON)
REAL(KIND=JPRB) :: ZCWP(KLON)
REAL(KIND=JPRB) :: ZWVP(KLON)
REAL(KIND=JPRB) :: ZTHETAS(KLON),ZLMO(KLON),ZHEATS(KLON),ZBLHG(KLON),ZUSTAR(KLON)
REAL(KIND=JPRB) :: ZRHO(KLON,KLEV),ZRHOFLUX(KLON,0:KLEV),ZCPFLUX(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZUPWP(KLON,0:KLEV),ZVPWP(KLON,0:KLEV),ZLIM(KLON)
REAL(KIND=JPRB) :: ZWPQP(KLON,0:KLEV),ZHEAT(KLON,0:KLEV),ZWT(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZRRTOTAL(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZWTHETAL(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZWPQLP(KLON,0:KLEV),ZWPQIP(KLON,0:KLEV),ZLWPQTP(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZWPQPS(KLON),ZLH(KLON,0:KLEV)
REAL(KIND=JPRB) :: ZRI(KLON,0:KLEV),ZDTRAD(KLON,KLEV)
REAL(KIND=JPRB) :: ZZUV,ZZUVB,ZALPHA,ZBETA,ZZ,ZDPHI,ZZRTI,ZCIS,ZDTETA,ZSTA
REAL(KIND=JPRB) :: ZMEAN_SAT_DEF(KLON),ZTOP(KLON),ZBASE(KLON),ZHCLA
REAL(KIND=JPRB) :: ZNEBMAX(KLON),ZMAXFRAC(KLON)
REAL(KIND=JPRB) :: ZDELTAPSG,ZVAL,ZDELTA,ZT

INTEGER(KIND=JPIM) :: JLEV,JLON,ILEV, IUSCM
REAL(KIND=JPRB) :: ZTHETABOT(KLON),ZRBOT(KLON),ZLCL(KLON)


REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

!     ------------------------------------------------------------------

#include "ecr1d.intfb.h"
#include "wrscmr.intfb.h"
#include "fcttrm.func.h"

!     ------------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('WRITEMUSC',0,ZHOOK_HANDLE)
ASSOCIATE(VKARMN=>YDML_PHY_MF%YRPHY0%VKARMN, &
 & TSPHY=>YDML_PHY_MF%YRPHY2%TSPHY, &
 & LMSE=>YDML_PHY_MF%YRARPHY%LMSE, &
 & LCONDWT=>YDML_PHY_MF%YRPHY%LCONDWT, LECTFL=>YDML_PHY_MF%YRPHY%LECTFL, LSOLV=>YDML_PHY_MF%YRPHY%LSOLV, &
 & LRAYFM=>YDML_PHY_MF%YRPHY%LRAYFM, LVGSN=>YDML_PHY_MF%YRPHY%LVGSN, LECT=>YDML_PHY_MF%YRPHY%LECT, &
 & LSNV=>YDML_PHY_MF%YRPHY%LSNV, &
 & LEFB=>YDML_PHY_MF%YRPHY%LEFB, LPROCLD=>YDML_PHY_MF%YRPHY%LPROCLD, &
 & RPI=>YDCST%RPI, RG=>YDCST%RG, RCPD=>YDCST%RCPD, RKAPPA=>YDCST%RKAPPA, RETV=>YDCST%RETV, &
 & RATM=>YDCST%RATM, RTT=>YDCST%RTT, RLVTT=>YDCST%RLVTT, RCPV=>YDCST%RCPV, &
 & RCW=>YDCST%RCW, RLSTT=>YDCST%RLSTT, RCS=>YDCST%RCS, RALPW=>YDCST%RALPW, &
 & RBETW=>YDCST%RBETW, RGAMW=>YDCST%RGAMW, RALPS=>YDCST%RALPS, RBETS=>YDCST%RBETS, &
 & RGAMS=>YDCST%RGAMS, RALPD=>YDCST%RALPD, RBETD=>YDCST%RBETD, RGAMD=>YDCST%RGAMD, RV=>YDCST%RV, &
 & P=>YDMF_PHYS%OUT, &
 & PT=>YDVARS%T%T0, PQ=>YDVARS%Q%T0, PQSN=>YDVARS%S%T0, PQR=>YDVARS%R%T0, &
 & PQG=>YDVARS%G%T0, PQL=>YDVARS%L%T0, PQI=>YDVARS%I%T0, PU=>YDVARS%U%T0, &
 & PV=>YDVARS%V%T0, PECT=>YDVARS%TKE%T0, PEFB1=>YDVARS%EFB1%T0, PEFB2=>YDVARS%EFB2%T0, PEFB3=>YDVARS%EFB3%T0, &
 & PAPRSF=>YDCPG_PHY0%PREHYDF, PAPRS=>YDCPG_PHY0%PREHYD, PDELP=>YDCPG_PHY0%XYB%DELP, &
 & PRDELP=>YDCPG_PHY0%XYB%RDELP, PALPH=>YDCPG_PHY0%XYB%ALPH, PLNPR=>YDCPG_PHY0%XYB%LNPR, &
 & PR=>YDCPG_DYN0%RCP%R, PCP=>YDCPG_DYN0%RCP%CP, PAPHI=>YDCPG_DYN0%PHI, PAPHIF=>YDCPG_DYN0%PHIF, PVERVEL=>YDCPG_DYN0%CTY%VVEL, &
 & PRH=>YDCPG_MISC%RH, PQS=>YDCPG_MISC%QS, PNEB=>YDCPG_MISC%NEB, PQLI=>YDCPG_MISC%QLI, PQICE=>YDCPG_MISC%QICE, &
 & PCLCT=>YDCPG_MISC%CLCT, &
 & YDSP_RR=>YDMF_PHYS_SURF%GSP_RR, YDSD_VV=>YDMF_PHYS_SURF%GSD_VV, YDSD_VF=>YDMF_PHYS_SURF%GSD_VF, &
 & YDSP_SG=>YDMF_PHYS_SURF%GSP_SG, YDSP_SB=>YDMF_PHYS_SURF%GSP_SB, &
 & YDRAD=>YDMF_PHYS%RAD, &
 & RSTATI=>YDRIP%RSTATI)
ASSOCIATE(PTS=>YDSP_RR%PT_T9,PTP=>YDSP_SB%PT_T9, PWL=>YDSP_RR%PFC_T9, PWP=>YDSP_SB%PQ_T9, &
 & PWPI=>YDSP_SB%PTL_T9, PWSI=>YDSP_RR%PIC_T9, PWS=>YDSP_RR%PW_T9)
!     ------------------------------------------------------------------

! * basic dimensioning (levels, time, vertical structure functions ...)

IUSCM=86
DO JLON=KIDIA,KFDIA
   DO JLEV=KTDIA,KLEV
      ZRHO(JLON,JLEV)=PAPRSF(JLON,JLEV)/(PR(JLON,JLEV)*PT(JLON,JLEV))
   ENDDO
ENDDO
CALL WRSCMR(IUSCM,'RHO',ZRHO,KLON,KLEV)
DO JLON=KIDIA,KFDIA
ZRHOFLUX(JLON,KLEV)=PAPRS(JLON,KLEV)/(PR(JLON,KLEV)*PT(JLON,KLEV))
ZRHOFLUX(JLON,0)=ZRHO(JLON,1)
ENDDO
DO JLEV=1,KLEV-1
DO JLON=KIDIA,KFDIA
      ZRHOFLUX(JLON,JLEV)=(PAPRSF(JLON,JLEV)*ZRHO(JLON,JLEV)+&
         &                 PAPRSF(JLON,JLEV+1)*ZRHO(JLON,JLEV+1))&
         &                /(PAPRSF(JLON,JLEV)+PAPRSF(JLON,JLEV+1))
ENDDO
ENDDO
CALL WRSCMR(IUSCM,'RHO_flux',ZRHOFLUX,KLON,KLEV+1)
CALL LFAECRI(IUSCM,'KLEV',KLEV,1)
CALL LFAECRR(IUSCM,'TSPHY',TSPHY,1)
!CALL WRSCMR(IUSCM,'VAH',VAH,KLON,KLEV+1)
!CALL WRSCMR(IUSCM,'VBH',VBH,KLON,KLEV+1)
CALL LFAECRI(IUSCM,'NINDAT',NINDAT,1)
CALL LFAECRI(IUSCM,'NSSSSS',NSSSSS,1)
!CALL LFAECRI(IUSCM,'KCLPH',KCLPH(2),1)
CALL LFAECRR(IUSCM,'RSTATI',RSTATI,1)


! * prognostic fields at time

CALL WRSCMR(IUSCM,'PU',PU,KLON,KLEV)
CALL WRSCMR(IUSCM,'PV',PV,KLON,KLEV)
ZVENT(:,:)=SQRT(PU(:,:)**2+PV(:,:)**2)
CALL WRSCMR(IUSCM,'PVENT',ZVENT,KLON,KLEV)
DO JLON=1,KLON
DO JLEV=1,KLEV
CALL RECPOL(PU(JLON,JLEV),PV(JLON,JLEV),ZVENT(JLON,JLEV),ZDIRVENT(JLON,JLEV))
ZDIRVENT(JLON,JLEV)=ZDIRVENT(JLON,JLEV)/RPI*180._JPRB
ZDIRVENT(JLON,JLEV)=270._JPRB-ZDIRVENT(JLON,JLEV)
IF (ZDIRVENT(JLON,JLEV) < 0.0_JPRB) THEN
   ZDIRVENT(JLON,JLEV)=360._JPRB+ZDIRVENT(JLON,JLEV)
ELSEIF (ZDIRVENT(JLON,JLEV) > 360.0_JPRB) THEN
   ZDIRVENT(JLON,JLEV)=ZDIRVENT(JLON,JLEV)-360._JPRB
ENDIF
ENDDO
ENDDO
CALL WRSCMR(IUSCM,'PDIRVENT',ZDIRVENT,KLON,KLEV)
CALL WRSCMR(IUSCM,'PT',PT,KLON,KLEV)
CALL WRSCMR(IUSCM,'PQ',PQ,KLON,KLEV)
ZH(:,:)=PCP(:,:)*PT(:,:)+PAPHIF(:,:) 
CALL WRSCMR(IUSCM,'ZH',ZH,KLON,KLEV)

!-------------------------------------------------
! Compute theta, theta_v and theta_vl.
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
DO JLEV=KTDIA,KLEV
   ZTHETA(JLON,JLEV)=THETA(PAPRSF(JLON,JLEV),PT(JLON,JLEV))
   ZTHETAV(JLON,JLEV)=THETAV(PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQ(JLON,JLEV))
   ZQC(JLON,JLEV)=PQLI(JLON,JLEV)+PQICE(JLON,JLEV)
   ZTHETAL_KE(JLON,JLEV)=THETAL_KE(PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQ(JLON,JLEV),ZQC(JLON,JLEV))
   !ZTHETAL(JLON,JLEV)=ZTHETA(JLON,JLEV)*(1.-(PLH(JLON,JLEV)*ZQC(JLON,JLEV))/(PCP(JLON,JLEV)*PT(JLON,JLEV)))
   ZTHETAL_LES(JLON,JLEV)=ZTHETA(JLON,JLEV)*(1.-(2.5E+06*ZQC(JLON,JLEV))/(1005.*PT(JLON,JLEV)))
   ZTHETAVL(JLON,JLEV)=THETAVL(PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQ(JLON,JLEV),ZQC(JLON,JLEV))
   ZELEVATION(JLON,JLEV)=(PAPHIF(JLON,JLEV)-PAPHI(JLON,KLEV))/RG
ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
   ZTHETA(JLON,KLEV+1)=THETA(PAPRS(JLON,KLEV),PTS(JLON))
   ZTHETAV(JLON,KLEV+1)=THETAV(PAPRS(JLON,KLEV),PTS(JLON),PQS(JLON))
   ZTHETAS(JLON)=ZTHETA(JLON,KLEV+1)
!   zthetavl(jlon,klev+1)=thetavl(paprs(jlon,klev),pts(jlon),pqs(jlon),zqc(jlon,klev))
   ZELEVATION(JLON,KLEV+1)=0.
ENDDO
CALL WRSCMR(IUSCM,'ZELEVA',ZELEVATION,KLON,KLEV)
CALL WRSCMR(IUSCM,'THETA',ZTHETA,KLON,KLEV)
CALL WRSCMR(IUSCM,'THETAV',ZTHETAV,KLON,KLEV)
CALL WRSCMR(IUSCM,'THETAL_KE',ZTHETAL_KE,KLON,KLEV)
CALL WRSCMR(IUSCM,'THETAL_BS',ZTHETAL_LES,KLON,KLEV)
!CALL WRSCMR(IUSCM,'THETAL',ZTHETAL,KLON,KLEV)
CALL WRSCMR(IUSCM,'THETAVL',ZTHETAVL,KLON,KLEV)
ZRI(:,:)=0._JPRB
DO JLEV=KTDIA,KLEV-1
DO JLON=KIDIA,KFDIA
     ZDPHI=PAPHIF(JLON,JLEV)-PAPHIF(JLON,JLEV+1)
     ZZRTI=2.0_JPRB/(PR(JLON,JLEV)*PT(JLON,JLEV)+PR(JLON,JLEV+1)*PT(JLON,JLEV+1))
     ZCIS=MAX(1.E-14_JPRB,(PU(JLON,JLEV)-PU(JLON,JLEV+1))**2+(PV(JLON,JLEV)-PV(JLON,JLEV+1))**2)
     ZDTETA=PR(JLON,JLEV)*PT(JLON,JLEV)-PR(JLON,JLEV+1)*PT(JLON,JLEV+1)+RKAPPA*ZDPHI
     ZSTA=ZDPHI*ZDTETA*ZZRTI
     ZRI(JLON,JLEV)=ZSTA/ZCIS
ENDDO
ENDDO
CALL WRSCMR(IUSCM,'ZRI',ZRI,KLON,KLEV+1)

IF (LAROME) THEN
   CALL WRSCMR(IUSCM,'PECT',PECT,KLON,KLEV)
ELSEIF (LECT) THEN
  IF (LECTFL) THEN
     CALL WRSCMR(IUSCM,'PECT',PECT,KLON,KLEV)
  ELSE         
     ZECT(:,:)=0._JPRB
     DO JLON=1,KLON
     DO JLEV=1,KLEV
        ZECT(JLON,JLEV)=PECT(JLON,JLEV)
     ENDDO
     ENDDO
     CALL WRSCMR(IUSCM,'PECT',ZECT,KLON,KLEV+1)
  ENDIF
ENDIF
IF (LEFB) THEN
   CALL WRSCMR(IUSCM,'PEFB1',PEFB1,KLON,KLEV)
   CALL WRSCMR(IUSCM,'PEFB2',PEFB2,KLON,KLEV)
   CALL WRSCMR(IUSCM,'PEFB3',PEFB3,KLON,KLEV)
ENDIF   
IF (LCONDWT.OR.LECMWF.OR.LAROME) THEN
  CALL WRSCMR(IUSCM,'PQI',PQI,KLON,KLEV)
  CALL WRSCMR(IUSCM,'PQL',PQL,KLON,KLEV)
  IF (LPROCLD.OR.LAROME) THEN
     CALL WRSCMR(IUSCM,'PQSN',PQSN,KLON,KLEV)
     CALL WRSCMR(IUSCM,'PQR',PQR,KLON,KLEV)
     IF (LAROME) THEN
        CALL WRSCMR(IUSCM,'PQG',PQG,KLON,KLEV)
     ENDIF
  ENDIF
ENDIF
! * dimensions used in physics

CALL LFAECRI(IUSCM,'KGL1',KGL1,1)
CALL LFAECRI(IUSCM,'KGL2',KGL2,1)
CALL LFAECRI(IUSCM,'KSGST',KSGST,1)
CALL LFAECRI(IUSCM,'KSTEP',KSTEP,1)
CALL LFAECRI(IUSCM,'KCSS',KCSS,1)
CALL ECR1D(IUSCM,'PMU0',PMU0,1,KLON)
CALL ECR1D(IUSCM,'PGEMU',PGEMU,1,KLON)
CALL ECR1D(IUSCM,'PGELAM',PGELAM,1,KLON)
! * input parameters for physics

CALL WRSCMR(IUSCM,'PAPHI',PAPHI,KLON,KLEV+1)
CALL WRSCMR(IUSCM,'PAPRS',PAPRS,KLON,KLEV+1)
CALL WRSCMR(IUSCM,'PAPHIF',PAPHIF,KLON,KLEV)
CALL WRSCMR(IUSCM,'PAPRSF',PAPRSF,KLON,KLEV)
CALL WRSCMR(IUSCM,'PALPH',PALPH,KLON,KLEV)
IF (LSOLV.OR.LECMWF) THEN
  CALL ECR1D(IUSCM,'PD2',YDSD_VV%PD2,1,KLON)
ENDIF
CALL WRSCMR(IUSCM,'PDELP',PDELP,KLON,KLEV)
IF (.NOT.LAROME) CALL ECR1D(IUSCM,'PGZ0H',P%GZ0H,1,KLON)
IF (.NOT.LAROME) CALL ECR1D(IUSCM,'PGZ0',P%GZ0,1,KLON)
IF (LSOLV) THEN
  CALL ECR1D(IUSCM,'PHV',YDSD_VV%PHV,1,KLON)
  CALL ECR1D(IUSCM,'PARG',YDSD_VV%PARG,1,KLON)
  CALL ECR1D(IUSCM,'PIVEG',YDSD_VV%PIVEG,1,KLON)
  CALL ECR1D(IUSCM,'PLAI',YDSD_VV%PLAI,1,KLON)
  CALL ECR1D(IUSCM,'PRSMIN',YDSD_VV%PRSMIN,1,KLON)
  CALL ECR1D(IUSCM,'PGZ0F',YDSD_VF%PZ0F,1,KLON)
  CALL ECR1D(IUSCM,'PGZ0HF',YDSD_VV%PZ0H,1,KLON)
  CALL ECR1D(IUSCM,'PSAB',YDSD_VV%PSAB,1,KLON)
  CALL ECR1D(IUSCM,'PALBF',YDSD_VF%PALBF,1,KLON)
  IF (LVGSN) THEN
     CALL ECR1D(IUSCM,'PALV',YDSD_VV%PALV,1,KLON)
     CALL ECR1D(IUSCM,'PALBH',YDSP_SG%PT_T1,1,KLON)
  ENDIF
ENDIF
IF(LSNV.AND..NOT.LECMWF )THEN
  CALL ECR1D(IUSCM,'PALBSF',YDSD_VF%PALBSF,1,KLON)
  CALL ECR1D(IUSCM,'PALBH',YDSP_SG%PT_T1,1,KLON)
ENDIF
CALL WRSCMR(IUSCM,'PLNPR',PLNPR,KLON,KLEV)
CALL WRSCMR(IUSCM,'PRDELP',PRDELP,KLON,KLEV)
CALL WRSCMR(IUSCM,'PCP',PCP,KLON,KLEV)
IF (.NOT.LAROME) CALL WRSCMR(IUSCM,'PCVGQ',PCVGQ,KLON,KLEV)
CALL WRSCMR(IUSCM,'PR',PR,KLON,KLEV)
!-------------------------------------------------
! Vertical velocity in pressure coordinate: omega/P (Pa/s).
!-------------------------------------------------

CALL WRSCMR(IUSCM,'PVERVEL',PVERVEL(:, 1:),KLON,KLEV)
!-------------------------------------------------
! Vertical velocity w (m/s).
!-------------------------------------------------

DO JLEV=1,KLEV
DO JLON=1,KLON
   ZW(JLON,JLEV)=-PR(JLON,JLEV)*PT(JLON,JLEV)/RG*PVERVEL(JLON,JLEV)
ENDDO
ENDDO
CALL WRSCMR(IUSCM,'ZW',ZW,KLON,KLEV)
DO JLEV=1,KLEV
DO JLON=1,KLON
   ZW(JLON,JLEV)=PAPRSF(JLON,JLEV)*PVERVEL(JLON,JLEV)
ENDDO
ENDDO
CALL WRSCMR(IUSCM,'ZOMEGA',ZW,KLON,KLEV)
!-------------------------------------------------
! ECMWF radiation.
!-------------------------------------------------

IF( LECMWF.OR.LRAYFM ) THEN
  CALL WRSCMR(IUSCM,'PEMTD',YDRAD%EMTD,KLON,KLEV+1)
  CALL WRSCMR(IUSCM,'PEMTU',YDRAD%EMTU,KLON,KLEV+1)
  CALL WRSCMR(IUSCM,'PTRSO',YDRAD%TRSW,KLON,KLEV+1)
ENDIF
CALL ECR1D(IUSCM,'PCLON',PCLON,1,KLON)
CALL ECR1D(IUSCM,'PSLON',PSLON,1,KLON)
CALL WRSCMR(IUSCM,'PFRSO',P%FRSO,KLON,KLEV+1)
CALL WRSCMR(IUSCM,'PFRTH',P%FRTH,KLON,KLEV+1)
DO JLEV=1,KLEV
    DO JLON=1,KLON
      ZDTRAD(JLON,JLEV)= RG*PRDELP(JLON,JLEV)/PCP(JLON,JLEV)*&
                         & (P%FRSO(JLON,JLEV-1,1)+P%FRTH(JLON,JLEV-1,1)-&
                         & P%FRSO(JLON,JLEV,1)+P%FRTH(JLON,JLEV,1))
    ENDDO
ENDDO
CALL WRSCMR(IUSCM,'ZDTRAD',ZDTRAD,KLON,KLEV)
CALL ECR1D(IUSCM,'PTCLS',P%TCLS,1,KLON)
CALL ECR1D(IUSCM,'PUCLS',P%UCLS,1,KLON)
CALL ECR1D(IUSCM,'PVCLS',P%VCLS,1,KLON)
CALL ECR1D(IUSCM,'PQCLS',P%QCLS,1,KLON)
CALL ECR1D(IUSCM,'PCLCT',PCLCT,1,KLON)
CALL ECR1D(IUSCM,'PCLCH',P%CLCH,1,KLON)
CALL ECR1D(IUSCM,'PCLCM',P%CLCM,1,KLON)
CALL ECR1D(IUSCM,'PCLCL',P%CLCL,1,KLON)
CALL ECR1D(IUSCM,'PCLCC',P%CLCC,1,KLON)
!ZVENTCLS(:)=SQRT(PUCLS(:)**2+PVCLS(:)**2)
DO JLON=1,KLON
CALL RECPOL(P%UCLS(JLON),P%VCLS(JLON),ZVENTCLS(JLON),ZDIRCLS(JLON))
ENDDO
ZDIRCLS(:)=ZDIRCLS(:)/RPI*180._JPRB
ZDIRCLS(:)=270._JPRB-ZDIRCLS(:)
DO JLON=1,KLON
IF (ZDIRCLS(JLON) < 0._JPRB) THEN
   ZDIRCLS(JLON)=360._JPRB+ZDIRCLS(JLON)
ELSEIF (ZDIRCLS(JLON) > 360.0_JPRB) THEN
   ZDIRCLS(JLON)=ZDIRCLS(JLON)-360._JPRB
ENDIF
ENDDO
CALL ECR1D(IUSCM,'VENTCLS',ZVENTCLS,1,KLON)
CALL ECR1D(IUSCM,'DIRCLS',ZDIRCLS,1,KLON)
CALL ECR1D(IUSCM,'PFCLL',P%FCLL,1,KLON)
CALL ECR1D(IUSCM,'PFCLN',P%FCLN,1,KLON)
CALL ECR1D(IUSCM,'PFCS',P%FCS,1,KLON)
CALL WRSCMR(IUSCM,'PNEB',PNEB,KLON,KLEV)
CALL ECR1D(IUSCM,'PCLPH',P%CLPH,1,KLON)
IF (.NOT.LAROME) THEN 
   CALL WRSCMR(IUSCM,'PQSAT',PQSAT,KLON,KLEV)
ENDIF
CALL ECR1D(IUSCM,'PRHCLS',P%RHCLS,1,KLON)

!-------------------------------------------------
! Perform some vertical integrals.
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
   ZECT_INT(JLON)=0._JPRB
   DO JLEV=KTDIA,KLEV
        ZECT_INT(JLON)=ZECT_INT(JLON)+PECT(JLON,JLEV)* PDELP(JLON,JLEV)/RG
   ENDDO
ENDDO
CALL ECR1D(IUSCM,'ECT_INT',ZECT_INT,1,KLON)
IF (LAROME) THEN
DO JLON=KIDIA,KFDIA
   ZLWP(JLON)=0._JPRB
   ZIWP(JLON)=0._JPRB
   ZWVP(JLON)=0._JPRB
   DO JLEV=KTDIA,KLEV
        ZLWP(JLON)=ZLWP(JLON)+PQL(JLON,JLEV)* PDELP(JLON,JLEV)/RG
        ZIWP(JLON)=ZIWP(JLON)+PQI(JLON,JLEV)*PDELP(JLON,JLEV)/RG
        ZWVP(JLON)=ZWVP(JLON)+PQ(JLON,JLEV)*PDELP(JLON,JLEV)/RG
    ENDDO
    ZCWP(JLON)=ZLWP(JLON)+ZIWP(JLON)
ENDDO
ELSE
DO JLON=KIDIA,KFDIA
   ZLWP(JLON)=0._JPRB
   ZIWP(JLON)=0._JPRB
   ZWVP(JLON)=0._JPRB
   DO JLEV=KTDIA,KLEV
        ZLWP(JLON)=ZLWP(JLON)+PQLI(JLON,JLEV)* PDELP(JLON,JLEV)/RG
        ZIWP(JLON)=ZIWP(JLON)+PQICE(JLON,JLEV)*PDELP(JLON,JLEV)/RG
        ZWVP(JLON)=ZWVP(JLON)+PQ(JLON,JLEV)*PDELP(JLON,JLEV)/RG
    ENDDO
    ZCWP(JLON)=ZLWP(JLON)+ZIWP(JLON)
ENDDO
ENDIF
CALL ECR1D(IUSCM,'LWP',ZLWP,1,KLON) ! liquid water path. Kg/m2
CALL ECR1D(IUSCM,'IWP',ZIWP,1,KLON) ! ice water path.
CALL ECR1D(IUSCM,'CWP',ZCWP,1,KLON) ! (liquid+ice) = condensated water path.
CALL ECR1D(IUSCM,'WVP',ZWVP,1,KLON) ! water vapour path.
CALL WRSCMR(IUSCM,'PFPLSL',P%FPLSL,KLON,KLEV+1)
CALL WRSCMR(IUSCM,'PFPLSN',P%FPLSN,KLON,KLEV+1)
IF (LAROME) THEN
   CALL WRSCMR(IUSCM,'PFPLSG',P%FPLSG,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPLSHL',P%FPLSH,KLON,KLEV+1)
   ZRRTOTAL(:,:)=P%FPLSN(:,:)+P%FPLSG(:,:)+P%FPLSH(:,:)
   CALL WRSCMR(IUSCM,'PREC_SOL',ZRRTOTAL,KLON,KLEV+1)
   ZRRTOTAL(:,:)=P%FPLSL(:,:)
   CALL WRSCMR(IUSCM,'PREC_LIQ',ZRRTOTAL,KLON,KLEV+1)
   ZRRTOTAL(:,:)=P%FPLSL(:,:)+P%FPLSN(:,:)+P%FPLSG(:,:)+P%FPLSH(:,:)
   CALL WRSCMR(IUSCM,'PREC_TOT',ZRRTOTAL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PQICE',PQI,KLON,KLEV)
   CALL WRSCMR(IUSCM,'PQLI',PQL,KLON,KLEV)
ELSE
   CALL WRSCMR(IUSCM,'PFPLCL',P%FPLCL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPLCN',P%FPLCN,KLON,KLEV+1)
   ZRRTOTAL(:,:)=P%FPLSN(:,:)+P%FPLCN(:,:)
   CALL WRSCMR(IUSCM,'PREC_SOL',ZRRTOTAL,KLON,KLEV+1)
   ZRRTOTAL(:,:)=P%FPLSL(:,:)+P%FPLCL(:,:)
   CALL WRSCMR(IUSCM,'PREC_LIQ',ZRRTOTAL,KLON,KLEV+1)
   ZRRTOTAL(:,:)=P%FPLSL(:,:)+P%FPLSN(:,:)+P%FPLCL(:,:)+P%FPLCN(:,:)
   CALL WRSCMR(IUSCM,'PREC_TOT',ZRRTOTAL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PQICE',PQICE,KLON,KLEV)
   CALL WRSCMR(IUSCM,'PQLI',PQLI,KLON,KLEV)
ENDIF

!  Paramters for EDMF intercomparison

ZTHETABOT(:)=ZTHETA(:,KLEV)
ZRBOT(:)=PQ(:,KLEV)/(1._JPRB-PQ(:,KLEV))
ZLCL(:)=(20._JPRB + (PT(:,KLEV)-273.15_JPRB)/5._JPRB)*(100._JPRB - PRH(:,KLEV))  

CALL ECR1D(IUSCM,'THETABOT',ZTHETABOT,1,KLON)
CALL ECR1D(IUSCM,'RBOT',ZRBOT,1,KLON)
CALL ECR1D(IUSCM,'LCL',ZLCL,1,KLON)

! END Paramters for EDMF intercomparison

CALL WRSCMR(IUSCM,'PRH',PRH,KLON,KLEV) ! relative humidity.
CALL ECR1D(IUSCM,'PTS',PTS,1,KLON)
CALL ECR1D(IUSCM,'PQS',PQS,1,KLON)
CALL ECR1D(IUSCM,'PFRSODS',P%FRSODS,1,KLON)
CALL ECR1D(IUSCM,'PFRTHDS',P%FRTHDS,1,KLON)
CALL ECR1D(IUSCM,'PITM',YDSD_VF%PLSM,1,KLON)
CALL WRSCMR(IUSCM,'PSTRTU',P%STRTU,KLON,KLEV+1)
CALL WRSCMR(IUSCM,'PSTRTV',P%STRTV,KLON,KLEV+1)
CALL WRSCMR(IUSCM,'PFRSOC',P%FRSOC,KLON,2)
CALL WRSCMR(IUSCM,'PFRTHC',P%FRTHC,KLON,2)
IF (.NOT.LAROME) THEN
   CALL ECR1D(IUSCM,'PALB',P%ALB,1,KLON)
   CALL WRSCMR(IUSCM,'PLH',PLH,KLON,KLEV)
   CALL WRSCMR(IUSCM,'PDIFCQ',P%DIFCQ,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PDIFCQI',P%DIFCQN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PDIFCQL',P%DIFCQL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PDIFCS',P%DIFCS,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PDIFTQ',P%DIFTQ,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PDIFTQI',P%DIFTQN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PDIFTQL',P%DIFTQL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PDIFTS',P%DIFTS,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCCQL',P%FCCQL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCCQN',P%FCCQN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCSQL',P%FCSQL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCSQN',P%FCSQN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCQVNG',P%FCQNG,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCQING',P%FCQNNG,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCQLNG',P%FCQLNG,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCQRNG',P%FCQRNG,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCQSNG',P%FCQSNG,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPFPSL',P%FPFPSL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPFPSL',P%FPFPSN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPFPSL',P%FPFPCL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPFPSL',P%FPFPCN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPEVPSL',P%FPEVPSL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPEVPSN',P%FPEVPSN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPEVPCL',P%FPEVPCL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFPEVPCN',P%FPEVPCN,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFTKE',PFTKE,KLON,KLEV+1)
   IF (LEFB) THEN
      CALL WRSCMR(IUSCM,'PFEFB1',PFEFB1,KLON,KLEV+1)
      CALL WRSCMR(IUSCM,'PFEFB2',PFEFB2,KLON,KLEV+1)
      CALL WRSCMR(IUSCM,'PFEFB3',PFEFB3,KLON,KLEV+1)
   ENDIF   
   CALL WRSCMR(IUSCM,'PSTRCU',P%STRCU,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PSTRCV',P%STRCV,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PSTRDU',P%STRDU,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PSTRDV',P%STRDV,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PSTRMU',P%STRMU,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PSTRMV',P%STRMV,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFRMH',P%FRMH,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFRMQ',PFRMQ,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'PFCHOZ',P%FCHOZ,KLON,KLEV+1)
ENDIF
IF (.NOT.LMSE) THEN
CALL ECR1D(IUSCM,'PLHS',PLHS,1,KLON)
ENDIF
!CALL ECR1D(IUSCM,'PEMIS',PEMIS,1,KLON)
CALL ECR1D(IUSCM,'PFEVL',P%FEVL,1,KLON)
!CALL ECR1D(IUSCM,'PFEVI',PFEVI,1,KLON)
CALL ECR1D(IUSCM,'PFEVN',P%FEVN,1,KLON)
IF ((.NOT.LMSE).AND.(LSOLV)) THEN  
  CALL ECR1D(IUSCM,'PCD',PCD,1,KLON)
  CALL ECR1D(IUSCM,'PCDN',PCDN,1,KLON)
  CALL ECR1D(IUSCM,'PCH',PCH,1,KLON)
  CALL ECR1D(IUSCM,'PSNS',YDSP_SG%PF_T9,1,KLON)
  CALL ECR1D(IUSCM,'PCPS',PCPS,1,KLON)
  CALL ECR1D(IUSCM,'PRS',PRS,1,KLON)
  CALL WRSCMR(IUSCM,'PLSCPE',PLSCPE,KLON,KLEV)
  CALL WRSCMR(IUSCM,'PQW',PQW,KLON,KLEV)
  CALL WRSCMR(IUSCM,'PTW',PTW,KLON,KLEV)
  CALL ECR1D(IUSCM,'PFCHSP',P%FCHSP,1,KLON)
!CALL ECR1D(IUSCM,'PEMISF',PEMISF,1,KLON)
!CALL ECR1D(IUSCM,'PGETRL',PGETRL,1,KLON)
  CALL ECR1D(IUSCM,'PVEG0',YDSD_VF%PVEG,1,KLON)
!CALL ECR1D(IUSCM,'PVRLAN',PVRLAN,1,KLON)
!CALL ECR1D(IUSCM,'PVRLDI',PVRLDI,1,KLON)
!CALL ECR1D(IUSCM,'POROG',POROG,1,KLON)
!CALL ECR1D(IUSCM,'PGM',PGM,1,KLON)
  CALL ECR1D(IUSCM,'PC1',PC1,1,KLON)
  CALL ECR1D(IUSCM,'PC2',PC2,1,KLON)
  CALL ECR1D(IUSCM,'PCT',P%CT,1,KLON)
  CALL ECR1D(IUSCM,'PFEVV',P%FEVV,1,KLON)
  CALL ECR1D(IUSCM,'PFTR',P%FTR,1,KLON)
  CALL ECR1D(IUSCM,'PFGEL',P%FGEL,1,KLON)
  CALL ECR1D(IUSCM,'PFGELS',P%FGELS,1,KLON)
  CALL ECR1D(IUSCM,'PFRSOPT',P%FRSOPT,1,KLON)
  CALL ECR1D(IUSCM,'PFRSOLU',P%FRSOLU,1,KLON)
  CALL ECR1D(IUSCM,'PNEIJ',PNEIJ,1,KLON)
  CALL ECR1D(IUSCM,'PVEG',PVEG0,1,KLON)
  CALL ECR1D(IUSCM,'PQSATS',PQSATS,1,KLON)
  CALL ECR1D(IUSCM,'PRUISL',P%RUISL,1,KLON)
  CALL ECR1D(IUSCM,'PFLWSP',P%FLWSP,1,KLON)
  CALL ECR1D(IUSCM,'PFONTE',P%FONTE,1,KLON)
  CALL ECR1D(IUSCM,'PFRSOPS',P%FRSOPS,1,KLON)
  CALL ECR1D(IUSCM,'PRUISP',P%RUISP,1,KLON)
  CALL ECR1D(IUSCM,'PRUISS',P%RUISS,1,KLON)
!CALL ECR1D(IUSCM,'PDCLS',PDCLS,1,KLON)
  CALL ECR1D(IUSCM,'PCAPE',P%CAPE,1,KLON)
  CALL ECR1D(IUSCM,'PCTOP',P%CTOP,1,KLON)
  CALL ECR1D(IUSCM,'PUGST',P%UGST,1,KLON)
  CALL ECR1D(IUSCM,'PVGST',P%VGST,1,KLON)
! * surface variables time T (ARPEGE style)
  CALL WRSCMR(IUSCM,'PTP',PTP,KLON,KCSS)
  CALL ECR1D(IUSCM,'PWL',PWL,1,KLON)
  CALL ECR1D(IUSCM,'PWP',PWP,1,KLON)
  CALL ECR1D(IUSCM,'PWPI',PWPI,1,KLON)
  CALL ECR1D(IUSCM,'PWSI',PWSI,1,KLON)
  CALL ECR1D(IUSCM,'PWS',PWS,1,KLON)
ENDIF
CALL LFAECRR(IUSCM,'VETAH',YDVETA%VETAH,KLEV+1)
CALL LFAECRR(IUSCM,'VETAF',YDVETA%VETAF,KLEV+2)
IF (LSNV.OR.LVGSN) THEN
  CALL ECR1D(IUSCM,'PALBNS',YDSP_SG%PA_T0,1,KLON)
  CALL ECR1D(IUSCM,'PRHONS',YDSP_SG%PR_T0,1,KLON)
ENDIF
IF (.NOT.LAROME) THEN
   DO JLON=KIDIA,KFDIA
   ZHEATS(JLON)= -(1./(ZRHO(JLON,KLEV)*PCP(JLON,KLEV))*P%FCS(JLON,1)+RETV*&
   &(P%FCLL(JLON,1)+P%FCLN(JLON,1))*P%TCLS(JLON)&
   &/(ZRHO(JLON,KLEV)*PLH(JLON,KLEV)))
   ENDDO
   CALL ECR1D(IUSCM,'ZHEATS',ZHEATS,1,KLON)
ENDIF

!-------------------------------------------------
! Surface values, to give as entry to the PBL depth computation.
!-------------------------------------------------

ILEV=KLEV+1
CALL ECR1D(IUSCM,'THETAS',ZTHETAS,1,KLON)
ZHCLA=HCLA(ILEV,ZTHETAVL,ZELEVATION,'DINT')
CALL LFAECRR(IUSCM,'HCLA',ZHCLA,1)

ZHCLA=HCLA(ILEV,ZTHETAVL,ZELEVATION,'AY1996')
CALL LFAECRR(IUSCM,'HCLA_AY1996',ZHCLA,1)

!   Diagnostic pour GABLS : - ZBLHG : boundary layer height
!                           - Surf. Potantial temperature flux  
!                           - Monin-Obukhov length

!ZCPFLUX(:,KLEV)=PCPS(:)
!ZCPFLUX(:,0)=PCP(:,1)
!DO JLON=KIDIA,KFDIA
!DO JLEV=KLEV-1,1,-1
!      ZCPFLUX(JLON,JLEV)=(PAPRSF(JLON,JLEV)*PCP(JLON,JLEV)+&
!         &                 PAPRSF(JLON,JLEV+1)*PCP(JLON,JLEV+1))&
!         &                /(PAPRSF(JLON,JLEV)+PAPRSF(JLON,JLEV+1))
!ENDDO
!ENDDO
!CALL WRSCMR(IUSCM,'ZCP_flux',ZCPFLUX,KLON,KLEV+1)
! Calcul de ZLH sur les niveaux flux
DO JLON=KIDIA,KFDIA
   ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PT(JLON,1)))
   ZLH(JLON,0)=FOLH (PT(JLON,1),ZDELTA)
   DO JLEV=2,KLEV
      ZT=(PT(JLON,JLEV-1)+PT(JLON,JLEV))*0.5_JPRB
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZT))
      ZLH(JLON,JLEV-1)=FOLH (ZT,ZDELTA)
   ENDDO
   ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PTS(JLON)))
   ZLH(JLON,KLEV)=FOLH (PTS(JLON),ZDELTA)
ENDDO
CALL WRSCMR(IUSCM,'ZLH_flux',ZLH,KLON,KLEV+1)
!
!   Calcul de Upwp, Vpwp et ZUSTAR
DO JLON=KIDIA,KFDIA
   ZUPWP(JLON,0)=0._JPRB
   ZVPWP(JLON,0)=0._JPRB
   DO JLEV=1,KLEV
      ZUPWP(JLON,JLEV)=-P%STRTU(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ZVPWP(JLON,JLEV)=-P%STRTV(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
   ENDDO
   ZUSTAR(JLON)=(ZUPWP(JLON,KLEV)**2+ZVPWP(JLON,KLEV)**2)**0.25
ENDDO
CALL WRSCMR(IUSCM,'ZUPWP_tur',ZUPWP,KLON,KLEV+1)
CALL WRSCMR(IUSCM,'ZVPWP_tur',ZVPWP,KLON,KLEV+1)
CALL ECR1D(IUSCM,'ZUSTAR',ZUSTAR,1,KLON)

IF (.NOT.LAROME) THEN
   DO JLON=KIDIA,KFDIA
      ZUPWP(JLON,0)=0._JPRB
      ZVPWP(JLON,0)=0._JPRB
      DO JLEV=1,KLEV
         ZUPWP(JLON,JLEV)=-P%STRCU(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
         ZVPWP(JLON,JLEV)=-P%STRCV(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ENDDO
   ENDDO
   CALL WRSCMR(IUSCM,'ZUPWP_dee',ZUPWP,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'ZVPWP_dee',ZVPWP,KLON,KLEV+1)
   DO JLON=KIDIA,KFDIA
   IF (ZHEATS(JLON) /= 0._JPRB) THEN
      ZLMO(JLON)=-ZUSTAR(JLON)**3*P%TCLS(JLON)/(VKARMN*RG*ZHEATS(JLON))
   ENDIF
   ENDDO
   CALL ECR1D(IUSCM,'ZLMO',ZLMO,1,KLON)

   !! Part Turbulente 
   DO JLON=KIDIA,KFDIA
   ZWPQP(JLON,0)=0._JPRB
   ZWPQLP(JLON,0)=0._JPRB
   ZWPQIP(JLON,0)=0._JPRB
   ZHEAT(JLON,0)=0._JPRB
   ZWT(JLON,0)=0._JPRB
   ZWTHETAL(JLON,0)=0._JPRB
      DO JLEV=1,KLEV
      ZWPQP(JLON,JLEV)=-P%DIFTQ(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ZWT(JLON,JLEV)=-P%DIFTS(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)&
        & /ZCPFLUX(JLON,JLEV)
      ZHEAT(JLON,JLEV)=-P%DIFTS(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)&
        & *(RATM/PAPRS(JLON,JLEV))**(RKAPPA)/RCPD
      ZWTHETAL(JLON,JLEV)=ZHEAT(JLON,JLEV) -(ZLH(JLON,JLEV)/RCPD)*&
      & (RATM/PAPRS(JLON,JLEV))**(RKAPPA)*(-P%DIFTQL(JLON,JLEV)&
        &  -P%DIFTQN(JLON,JLEV))/ZRHOFLUX(JLON,JLEV)
      ZWPQLP(JLON,JLEV)=-P%DIFTQL(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ZWPQIP(JLON,JLEV)=-P%DIFTQN(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ENDDO
      ZWPQPS(JLON)=-(P%FEVL(JLON,1)+P%FEVN(JLON,1))/ZRHOFLUX(JLON,KLEV)
   ENDDO
   ZLWPQTP(:,:)=ZWPQLP(:,:)+ZWPQIP(:,:)+ZWPQP(:,:)
   CALL WRSCMR(IUSCM,'ZWPQT_tur',ZLWPQTP,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'WTHL_tur',ZWTHETAL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'ZWPQP_tur',ZWPQP,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'ZHEAT_tur',ZHEAT,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'ZWT_tur',ZWT,KLON,KLEV+1)
   CALL ECR1D(IUSCM,'ZWPQPS',ZWPQPS,1,KLON)
   !! Part Deep Convection
   DO JLON=KIDIA,KFDIA
   ZWPQP(JLON,0)=0._JPRB
   ZWPQLP(JLON,0)=0._JPRB
   ZWPQIP(JLON,0)=0._JPRB
   ZHEAT(JLON,0)=0._JPRB
   ZWT(JLON,0)=0._JPRB
   ZWTHETAL(JLON,0)=0._JPRB
      DO JLEV=1,KLEV
      ZWPQP(JLON,JLEV)=-P%DIFCQ(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ZWT(JLON,JLEV)=-P%DIFCS(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)&
        & /ZCPFLUX(JLON,JLEV)
      ZHEAT(JLON,JLEV)=-P%DIFCS(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)&
        & *(RATM/PAPRS(JLON,JLEV))**(RKAPPA)/RCPD
      ZWTHETAL(JLON,JLEV)=ZHEAT(JLON,JLEV) -(ZLH(JLON,JLEV)/RCPD)*&
      & (RATM/PAPRS(JLON,JLEV))**(RKAPPA)*(-P%DIFCQL(JLON,JLEV)&
        &  -P%DIFCQN(JLON,JLEV))/ZRHOFLUX(JLON,JLEV)
      ZWPQLP(JLON,JLEV)=-P%DIFCQL(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ZWPQIP(JLON,JLEV)=-P%DIFCQN(JLON,JLEV)/ZRHOFLUX(JLON,JLEV)
      ENDDO
   ENDDO
   ZLWPQTP(:,:)=ZWPQLP(:,:)+ZWPQIP(:,:)+ZWPQP(:,:)
   CALL WRSCMR(IUSCM,'ZWPQT_dee',ZLWPQTP,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'WTHL_dee',ZWTHETAL,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'ZWPQP_dee',ZWPQP,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'ZHEAT_dee',ZHEAT,KLON,KLEV+1)
   CALL WRSCMR(IUSCM,'ZWT_dee',ZWT,KLON,KLEV+1)

   DO JLON=KIDIA,KFDIA
   ZLIM(JLON)=0.05*ZUSTAR(JLON)**2
   ZBLHG(JLON)=(PAPHIF(JLON,KLEV)-PAPHI(JLON,KLEV))/RG
   ZZUVB=ZLIM(JLON)
   ZZ=0.
   DO JLEV=KLEV-1,0,-1
      ZZUV=SQRT(ZUPWP(JLON,JLEV)**2+ZVPWP(JLON,JLEV)**2)
      IF ((ZZUV<=ZLIM(JLON)).AND.(ZZ==0.)) THEN
         ZALPHA=(ZZUVB-ZZUV)*RG/(PAPHI(JLON,JLEV+1)-PAPHI(JLON,JLEV))
         ZBETA=ZZUV-ZALPHA*(PAPHI(JLON,JLEV)-PAPHI(JLON,KLEV))/RG
         ZBLHG(JLON)= (ZLIM(JLON)-ZBETA)/MAX(ZALPHA,0.0000001_JPRB)
         ZZ=1.
      ENDIF
      ZZUVB=ZZUV
   ENDDO
   ENDDO
   CALL ECR1D(IUSCM,'ZBLHG',ZBLHG,1,KLON)
ENDIF

!-------------------------------------------------
! Vertical mean of saturation deficit (thetaES - thetaE) between 0 and 15 km.
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
   ZMEAN_SAT_DEF(JLON)=0._JPRB
   ZDELTAPSG=0._JPRB
   DO JLEV=KTDIA,KLEV
      IF (PAPHIF(JLON,JLEV)/RG < 15000._JPRB ) THEN
         ZVAL=THETAES_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV))&
                & -THETAE_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQ(JLON,JLEV))
         ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
         ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
      ENDIF
   ENDDO
   ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
ENDDO
CALL ECR1D(IUSCM,'SAT_DEF_0-15',ZMEAN_SAT_DEF,1,KLON) ! vertical mean of saturation deficit.

!-------------------------------------------------
! Vertical mean of saturation deficit (thetaES - thetaE) between 1 and 15 km.
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
   ZMEAN_SAT_DEF(JLON)=0._JPRB
   ZDELTAPSG=0._JPRB
   DO JLEV=KTDIA,KLEV
      IF (PAPHIF(JLON,JLEV)/RG > 1000._JPRB .AND.&
         & PAPHIF(JLON,JLEV)/RG < 15000._JPRB ) THEN
         ZVAL=THETAES_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV))&
             & -THETAE_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQ(JLON,JLEV))
         ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
         ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
      ENDIF
   ENDDO
   IF (ZDELTAPSG/=0._JPRB) ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
ENDDO
CALL ECR1D(IUSCM,'SAT_DEF_1-15',ZMEAN_SAT_DEF,1,KLON) ! vertical mean of saturation deficit.

!-------------------------------------------------
! Vertical mean of saturation deficit (thetaES - thetaE) between 2.5 and 4.5 km.
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
   ZMEAN_SAT_DEF(JLON)=0._JPRB
   ZDELTAPSG=0._JPRB
   DO JLEV=KTDIA,KLEV
      IF(PAPHIF(JLON,JLEV)/RG > 2500._JPRB .AND.&
        & PAPHIF(JLON,JLEV)/RG < 4500._JPRB ) THEN
        ZVAL=THETAES_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV))&
           & -THETAE_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQ(JLON,JLEV))
        ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
        ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
      ENDIF
    ENDDO
    IF (ZDELTAPSG/=0._JPRB) ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
ENDDO
CALL ECR1D(IUSCM,'SAT_DEF_2.5-4.5',ZMEAN_SAT_DEF,1,KLON) ! vertical mean of saturation deficit.

!-------------------------------------------------
! Vertical mean of saturation deficit (thetaES - thetaE) between 1.4 and 3.0 km.
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
   ZMEAN_SAT_DEF(JLON)=0._JPRB
   ZDELTAPSG=0._JPRB
   DO JLEV=KTDIA,KLEV
      IF(PAPHIF(JLON,JLEV)/RG > 1400._JPRB .AND.&
        & PAPHIF(JLON,JLEV)/RG < 3000._JPRB ) THEN
        ZVAL=THETAES_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV))&
           & -THETAE_BOLTON(PAPRSF(JLON,JLEV),PT(JLON,JLEV),PQ(JLON,JLEV))
        ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
        ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
      ENDIF
   ENDDO
   IF (ZDELTAPSG/=0._JPRB) ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
ENDDO
CALL ECR1D(IUSCM,'SAT_DEF_1.4-3',ZMEAN_SAT_DEF,1,KLON) ! vertical mean of saturation deficit.

!!-------------------------------------------------
!! Vertical mean of saturation deficit (qvsat - qv) between 0 and 15 km.
!!-------------------------------------------------
!
!DO JLON=KIDIA,KFDIA
!   ZMEAN_SAT_DEF(JLON)=0._JPRB
!   ZDELTAPSG=0._JPRB
!   DO JLEV=KTDIA,KLEV
!      IF(PAPHIF(JLON,JLEV)/RG >= 0._JPRB .AND.&
!        & PAPHIF(JLON,JLEV)/RG < 15000._JPRB ) THEN
!        ZVAL=(PQSAT(JLON,JLEV)-PQ(JLON,JLEV))*1000._JPRB
!        ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
!        ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
!      ENDIF
!   ENDDO
!   IF (ZDELTAPSG/=0._JPRB) ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
!ENDDO
!CALL ECR1D(IUSCM,'SAT_DEF_QV_0-15',ZMEAN_SAT_DEF,1,KLON)
!
!!-------------------------------------------------
!! Vertical mean of saturation deficit (qvsat - qv) between 1 and 15 km.
!!-------------------------------------------------
!
!DO JLON=KIDIA,KFDIA
!   ZMEAN_SAT_DEF(JLON)=0._JPRB
!   ZDELTAPSG=0._JPRB
!   DO JLEV=KTDIA,KLEV
!      IF(PAPHIF(JLON,JLEV)/RG >= 1000._JPRB .AND.&
!        & PAPHIF(JLON,JLEV)/RG < 15000._JPRB ) THEN
!        ZVAL=(PQSAT(JLON,JLEV)-PQ(JLON,JLEV))*1000._JPRB
!        ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
!        ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
!      ENDIF
!   ENDDO
!   IF (ZDELTAPSG/=0.) ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
!ENDDO
!CALL ECR1D(IUSCM,'SAT_DEF_QV_1-15',ZMEAN_SAT_DEF,1,KLON)
!
!!-------------------------------------------------
!! Vertical mean of saturation deficit (qvsat - qv) between 2.5 and 4.5 km.
!!-------------------------------------------------
!
!DO JLON=KIDIA,KFDIA
!   ZMEAN_SAT_DEF(JLON)=0._JPRB
!   ZDELTAPSG=0._JPRB
!   DO JLEV=KTDIA,KLEV
!      IF(PAPHIF(JLON,JLEV)/RG >= 2500._JPRB&
!        &.AND. PAPHIF(JLON,JLEV)/RG < 4500._JPRB ) THEN
!        ZVAL=(PQSAT(JLON,JLEV)-PQ(JLON,JLEV))*1000.
!        ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
!        ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
!      ENDIF
!   ENDDO
!   IF (ZDELTAPSG/=0._JPRB) ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
!ENDDO
!CALL ECR1D(IUSCM,'SAT_DEF_QV_2.5-4.5',ZMEAN_SAT_DEF,1,KLON)
!
!!-------------------------------------------------
!! Vertical mean of saturation deficit (qvsat - qv) between 1.4 and 3 km.
!!-------------------------------------------------
!
!DO JLON=KIDIA,KFDIA
!   ZMEAN_SAT_DEF(JLON)=0._JPRB
!   ZDELTAPSG=0._JPRB
!   DO JLEV=KTDIA,KLEV
!      IF(PAPHIF(JLON,JLEV)/RG >= 1400._JPRB&
!        & .AND. PAPHIF(JLON,JLEV)/RG < 3000._JPRB ) THEN
!        ZVAL=(PQSAT(JLON,JLEV)-PQ(JLON,JLEV))*1000._JPRB
!        ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)+ZVAL*PDELP(JLON,JLEV)/RG
!        ZDELTAPSG=ZDELTAPSG+PDELP(JLON,JLEV)/RG
!      ENDIF
!   ENDDO
!   IF (ZDELTAPSG/=0._JPRB) ZMEAN_SAT_DEF(JLON)=ZMEAN_SAT_DEF(JLON)/ZDELTAPSG
!ENDDO
!CALL ECR1D(IUSCM,'SAT_DEF_QV_1.4-3',ZMEAN_SAT_DEF,1,KLON)

!-------------------------------------------------
! Base and Top of PBL clouds 
!-------------------------------------------------

DO JLON=KIDIA,KFDIA
   ZTOP(JLON)=0._JPRB
   ZBASE(JLON)=0._JPRB
   ZNEBMAX(JLON)=0._JPRB
   ZMAXFRAC(JLON)=0._JPRB
   DO JLEV=KLEV,KTDIA,-1
      IF(PNEB(JLON,JLEV) > 0.01_JPRB .AND.&
     &  ((PAPHIF(JLON,JLEV)/RG) < 5000._JPRB) .AND.&
     &  ((PAPHIF(JLON,JLEV)/RG) > 50._JPRB)) THEN 
        ZTOP(JLON)=PAPHIF(JLON,JLEV)/RG
      ENDIF
      IF ((PNEB(JLON,JLEV) > ZNEBMAX(JLON)).AND.&
      &   ((PAPHIF(JLON,JLEV)/RG) < 5000._JPRB)) THEN
        ZMAXFRAC(JLON)=PAPHIF(JLON,JLEV)/RG
        ZNEBMAX(JLON)=PNEB(JLON,JLEV)
      ENDIF
   ENDDO
   DO JLEV=KTDIA,KLEV
      IF(PNEB(JLON,JLEV) > 0.01_JPRB .AND.&
     &  ((PAPHIF(JLON,JLEV)/RG) > 50._JPRB)) THEN 
        ZBASE(JLON)=PAPHIF(JLON,JLEV)/RG
      ENDIF
   ENDDO
ENDDO
CALL ECR1D(IUSCM,'ZCLDTOP',ZTOP,1,KLON) 
CALL ECR1D(IUSCM,'ZCLDBAS',ZBASE,1,KLON) 
CALL ECR1D(IUSCM,'ZMAXFRAC',ZMAXFRAC,1,KLON) 
CALL ECR1D(IUSCM,'ZNEBMAX',ZNEBMAX,1,KLON) 

!     ------------------------------------------------------------------
END ASSOCIATE
END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('WRITEMUSC',1,ZHOOK_HANDLE)
END SUBROUTINE WRITEMUSC
